plugins {
    id 'java-library'
    id 'jacoco'

    id 'maven-publish'
    id 'signing'
}

group 'io.github.werthersechte'
version '1.4.3'

sourceCompatibility = 16

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.discord4j:discord4j-core:3.2.3"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

    testImplementation 'org.assertj:assertj-core:3.24.2'

    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.2.0'

    testImplementation 'io.cucumber:cucumber-java:7.11.2'
    testImplementation 'io.cucumber:cucumber-junit:7.11.2'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.2'
}

test {
    useJUnitPlatform()
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test
    reports {
        csv.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/ai/**',
                              '**/gui/**'])
        }))
    }
}

// maven central deploy

java {
    withSourcesJar()
    withJavadocJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            groupId = project.group.toString()
            artifactId = project.name
            version = project.version.toString()

            from components.java

            pom {
                name = project.name
                description = "A cathedral implementation in Java"
                url = "https://github.com/WerthersEchte/cathedral"
                licenses {
                    license {
                        name = "The Unlicense"
                        url = "https://unlicense.org"
                    }
                }
                developers {
                    developer {
                        id = "WerthersEchte"
                        name = "Eike Petersen"
                        email = "epetersen@gmx.li"
                    }
                }
                scm {
                    url = "https://github.com/WerthersEchte/cathedral"

                    connection = "scm:git:git://github.com/WerthersEchte/cathedral.git"

                    developerConnection = "scm:git:git://github.com/WerthersEchte/cathedral.git"

                }
                issueManagement {
                    url = "https://github.com/WerthersEchte/cathedral/issues"
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }

            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}